apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
// 通用配置
android {

    // 编译源码版本
    def androidapi = 29
    compileSdkVersion androidapi
    defaultConfig {
        // 最低安装版本
        minSdkVersion 21
        // 目标适配版本
        targetSdkVersion androidapi
        versionName "20230713"
        versionCode 1
    }

    // 支持 Java JDK 8
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    // 设置存放 so 文件的目录
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    // 可在 Studio 最左侧中的 Build Variants 选项中切换默认的构建类型
    buildTypes {
        // 调试版本
        debug {}
        // 预览版本
        preview {}
        // 正式版本
        release {}
    }

    // 代码警告配置
    lintOptions {
        // 禁用文本硬编码警告
        disable 'HardcodedText'
        // 禁用图片描述警告
        disable 'ContentDescription'
    }
}

afterEvaluate {
    // 前提条件是这个 Module 工程必须是 Library 类型，并且排除名为 umeng 的 Module 工程
    if (android.defaultConfig.applicationId == null && "umeng" != getName()) {
        // 排除 BuildConfig.class
        generateReleaseBuildConfig.enabled = false
        generatePreviewBuildConfig.enabled = false
        generateDebugBuildConfig.enabled = false
    }
}

dependencies {
    // 依赖 libs 目录下所有的 jar 和 aar 包
    implementation fileTree(include: ['*.jar', '*.aar'], dir: 'libs')

    // AndroidX 库：https://github.com/androidx/androidx
    implementation 'androidx.appcompat:appcompat:1.3.1'
    // Material 库：https://github.com/material-components/material-components-android
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    // Kotlin 协程：https://github.com/Kotlin/kotlinx.coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'com.tencent:mmkv-static:1.2.10'
//    https://github.com/kongzue/DialogX/releases
    implementation 'com.github.kongzue.DialogX:DialogX:0.0.48'
    api 'com.blankj:utilcode:1.30.6'
    def work_version = "2.5.0"
    // (Java only)https://developer.android.google.cn/topic/libraries/architecture/workmanager/how-to/define-work?hl=zh-cn#java
    implementation "androidx.work:work-runtime:$work_version"
}